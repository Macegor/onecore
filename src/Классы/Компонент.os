#Использовать fs

Перем Имя;
Перем ИмяЛатинское;
Перем ПутьКомпонента;
Перем МодульКомпонента;

// Перем Модели;
// Перем Представления;
// Перем Сервисы;
// Перем Ресурсы;
// Перем Шаблоны;

Процедура ПриСозданииОбъекта(ИмяКомпонента, ИмяКомпонентаЛатинское, Отказ = Ложь)

	Имя = ИмяКомпонента;
	ИмяЛатинское = ИмяКомпонентаЛатинское;
	КаталогПриложения = Служебное.КаталогПриложения();
	ПутьКомпонента = ОбъединитьПути(КаталогПриложения, ИмяКомпонента);
	ИмяМетодаИнициализации = "ПриИнициализацииКомпонента";
	
	// ПроинициализироватьКоллекцииКомпонента();

	Если НЕ ФС.КаталогСуществует(ПутьКомпонента) Тогда
		Лог.Отладка(СтрШаблон("Компонент ""%1"" не найден по пути: %2", ИмяКомпонента, ПутьКомпонента));
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ПутьСценарияКомпонента = ОбъединитьПути(ПутьКомпонента, "МодульКомпонента.os");

	Если НЕ ФС.Существует(ПутьСценарияКомпонента) Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" не найден модуль компонента по пути: %2", ИмяКомпонента, ПутьСценарияКомпонента));
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	МодульКомпонента = ЗагрузитьСценарий(ПутьСценарияКомпонента);

	Если Рефлексия.МетодСуществует(МодульКомпонента, ИмяМетодаИнициализации) Тогда
		Рефлексия.ВызватьМетод(МодульКомпонента, ИмяМетодаИнициализации);
	КонецЕсли;

	ПодключитьМоделиКомпонента();
	ПодключитьПредставленияКомпонента();

КонецПроцедуры

Функция Имя() Экспорт
	Возврат Имя;
КонецФункции

Функция ИмяЛатинское() Экспорт
	Возврат ИмяЛатинское;
КонецФункции

Функция МодульКомпонента() Экспорт
	Возврат МодульКомпонента;
КонецФункции

// Процедура ПроинициализироватьКоллекцииКомпонента()

// 	Модели = Новый ТаблицаЗначений();
// 	Представления = Новый ТаблицаЗначений();
// 	Сервисы = Новый ТаблицаЗначений();
// 	Ресурсы = Новый ТаблицаЗначений();
// 	Шаблоны = Новый ТаблицаЗначений();	

// КонецПроцедуры

Процедура ПодключитьМоделиКомпонента()

	МенеджерОбъектов = Приложение.МенеджерОбъектов();
	ПутьКМоделям = ОбъединитьПути(ПутьКомпонента, "Модели");

	Если НЕ ФС.КаталогСуществует(ПутьКМоделям) Тогда
		Лог.Предупреждение(СтрШаблон("В компоненте ""%1"" отсутствует каталог с моделями", Имя));
		Возврат;
	КонецЕсли;

	ФайлыМоделей = НайтиФайлы(ПутьКМоделям, "*.os", Истина);

	Если НЕ ФайлыМоделей.Количество() Тогда
		Лог.Предупреждение(СтрШаблон("В компоненте ""%1"" отсутствуют модели", Имя));
	КонецЕсли;

	Для каждого Файл Из ФайлыМоделей Цикл
		КлючОбъекта = Служебное.ПодготовитьКлючОбъекта(Файл, ПутьКМоделям);
		МенеджерОбъектов.ЗарегистрироватьОбъект(Файл.ПолноеИмя, КлючОбъекта);
		Лог.Отладка(СтрШаблон("Загружена модель ""%1""", Файл.ИмяБезРасширения));
	КонецЦикла;

КонецПроцедуры

Процедура ПодключитьПредставленияКомпонента()
	
	МенеджерОбъектов = Приложение.МенеджерОбъектов();
	ПутьКПредставлениям = ОбъединитьПути(ПутьКомпонента, "Представления");

	Если НЕ ФС.КаталогСуществует(ПутьКПредставлениям) Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" отсутствует каталог с представлениями", Имя));
		Возврат;
	КонецЕсли;

	ФайлыПредставлений = НайтиФайлы(ПутьКПредставлениям, "*.os", Истина);

	Если НЕ ФайлыПредставлений.Количество() Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" отсутствуют представления", Имя));
	КонецЕсли;

	Для каждого Файл Из ФайлыПредставлений Цикл
		КлючОбъекта = Служебное.ПодготовитьКлючОбъекта(Файл, ПутьКПредставлениям);
		МенеджерОбъектов.ЗарегистрироватьОбъект(Файл.ПолноеИмя, КлючОбъекта);
		Лог.Отладка(СтрШаблон("Загружено представление ""%1""", Файл.ИмяБезРасширения));
	КонецЦикла;

КонецПроцедуры