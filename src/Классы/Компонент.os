#Использовать fs

Перем Имя;
Перем ИмяЛатинское;
Перем ПутьКомпонента;
Перем МодульКомпонента;
Перем ОписанияТаблицМоделей;

Процедура ПриСозданииОбъекта(ИмяКомпонента, ИмяКомпонентаЛатинское, Отказ = Ложь)

	Имя = ИмяКомпонента;
	ИмяЛатинское = ИмяКомпонентаЛатинское;
	КаталогПриложения = Служебное.КаталогПриложения();
	ПутьКомпонента = ОбъединитьПути(КаталогПриложения, ИмяКомпонента);
	ИмяМетодаИнициализации = "ПриИнициализацииКомпонента";

	ОписанияТаблицМоделей = Новый ТаблицаЗначений();
	ОписанияТаблицМоделей.Колонки.Добавить("КлючОбъекта", Новый ОписаниеТипов("Строка"));
	ОписанияТаблицМоделей.Колонки.Добавить("ОписаниеМодели", Новый ОписаниеТипов("ОписаниеТаблицыМодели"));
	
	Если НЕ ФС.КаталогСуществует(ПутьКомпонента) Тогда
		Лог.Отладка(СтрШаблон("Компонент ""%1"" не найден по пути: %2", ИмяКомпонента, ПутьКомпонента));
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ПутьСценарияКомпонента = ОбъединитьПути(ПутьКомпонента, "МодульКомпонента.os");

	Если НЕ ФС.Существует(ПутьСценарияКомпонента) Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" не найден модуль компонента по пути: %2", ИмяКомпонента, ПутьСценарияКомпонента));
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	МодульКомпонента = ЗагрузитьСценарий(ПутьСценарияКомпонента);

	Если Рефлексия.МетодСуществует(МодульКомпонента, ИмяМетодаИнициализации) Тогда
		Рефлексия.ВызватьМетод(МодульКомпонента, ИмяМетодаИнициализации);
	КонецЕсли;

	ПодключитьМоделиКомпонента();
	ПодключитьПредставленияКомпонента();
	ПодключитьСервисыКомпонента();

КонецПроцедуры

Функция Имя() Экспорт
	Возврат Имя;
КонецФункции

Функция ИмяЛатинское() Экспорт
	Возврат ИмяЛатинское;
КонецФункции

Функция МодульКомпонента() Экспорт
	Возврат МодульКомпонента;
КонецФункции

Функция ОписанияТаблицМоделей() Экспорт
	Возврат ОписанияТаблицМоделей;
КонецФункции

Процедура ПодключитьМоделиКомпонента()

	МенеджерБазыДанных = Приложение.МенеджерБазыДанных();

	Если МенеджерБазыДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МенеджерОбъектов = Приложение.МенеджерОбъектов();
	ПутьКМоделям = ОбъединитьПути(ПутьКомпонента, "Модели");

	Если НЕ ФС.КаталогСуществует(ПутьКМоделям) Тогда
		Лог.Предупреждение(СтрШаблон("В компоненте ""%1"" отсутствует каталог с моделями", Имя));
		Возврат;
	КонецЕсли;

	ФайлыМоделей = НайтиФайлы(ПутьКМоделям, "*.os", Истина);

	Если НЕ ФайлыМоделей.Количество() Тогда
		Лог.Предупреждение(СтрШаблон("В компоненте ""%1"" отсутствуют модели", Имя));
	КонецЕсли;

	Для каждого Файл Из ФайлыМоделей Цикл

		КлючОбъекта = Служебное.ПодготовитьКлючОбъекта(Файл, ПутьКМоделям);
		МенеджерОбъектов.ЗарегистрироватьОбъект(Файл.ПолноеИмя, КлючОбъекта, Истина);
		ОбъектМодели = МенеджерОбъектов.СоздатьОбъект(КлючОбъекта);

		Если НЕ МенеджерБазыДанных.ОбъектЯвляетсяМоделью(ОбъектМодели) Тогда
			Лог.Предупреждение(СтрШаблон("Модель ""%1"" не зарегистрирована", Файл.ИмяБезРасширения));
			Продолжить;
		КонецЕсли;

		ИмяТаблицы = СтрШаблон("%1_%2", ИмяЛатинское, Рефлексия.АннотацииМетода(ОбъектМодели, "ПриСозданииОбъекта").Модель.Имя);
		ИмяМодели = Строка(Тип(ОбъектМодели));	
		ОписаниеМодели = Новый ОписаниеТаблицыМодели(ИмяМодели, ИмяТаблицы, ЭтотОбъект);

		Свойства = Рефлексия.ПолучитьТаблицуСвойств(ОбъектМодели);
	
		Для каждого СтрокаСвойства Из Свойства Цикл

			ИмяПоля = СтрокаСвойства.Имя;
			АннотацииСвойства = Рефлексия.АннотацииСвойства(ОбъектМодели, ИмяПоля);
			ИмяКолонки = "";
			Тип = "";
			ПараметрыТипа = Новый Структура();
			ЭтоПервичныйКлюч = АннотацииСвойства.Свойство("Идентификатор");

			Если ЭтоПервичныйКлюч Тогда
				ПараметрыТипа.Вставить("ПервичныйКлюч", ЭтоПервичныйКлюч);
			КонецЕсли;

			Для каждого КлючЗначение Из АннотацииСвойства.Поле Цикл

				Если КлючЗначение.Ключ = "Имя" Тогда
					ИмяКолонки = КлючЗначение.Значение;
				ИначеЕсли КлючЗначение.Ключ = "Тип" Тогда
					Тип = Тип(КлючЗначение.Значение);
				Иначе
					ПараметрыТипа.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
				КонецЕсли;

			КонецЦикла;

			ОписаниеМодели.ДобавитьПоле(ИмяПоля, ИмяКолонки, Тип, ПараметрыТипа);

		КонецЦикла;
		
		ЗаписьОписания = ОписанияТаблицМоделей.Добавить();
		ЗаписьОписания.КлючОбъекта = КлючОбъекта;
		ЗаписьОписания.ОписаниеМодели = ОписаниеМодели;

		Лог.Отладка(СтрШаблон("Зарегистрирована модель ""%1""", Файл.ИмяБезРасширения));

		ОсвободитьОбъект(ОбъектМодели);

	КонецЦикла;

КонецПроцедуры

Процедура ПодключитьПредставленияКомпонента()
	
	МенеджерОбъектов = Приложение.МенеджерОбъектов();
	ПутьКПредставлениям = ОбъединитьПути(ПутьКомпонента, "Представления");

	Если НЕ ФС.КаталогСуществует(ПутьКПредставлениям) Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" отсутствует каталог с представлениями", Имя));
		Возврат;
	КонецЕсли;

	ФайлыПредставлений = НайтиФайлы(ПутьКПредставлениям, "*.os", Истина);

	Если НЕ ФайлыПредставлений.Количество() Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" отсутствуют представления", Имя));
	КонецЕсли;

	Для каждого Файл Из ФайлыПредставлений Цикл
		КлючОбъекта = Служебное.ПодготовитьКлючОбъекта(Файл, ПутьКПредставлениям);
		МенеджерОбъектов.ЗарегистрироватьОбъект(Файл.ПолноеИмя, КлючОбъекта);
		Лог.Отладка(СтрШаблон("Загружено представление ""%1""", Файл.ИмяБезРасширения));
	КонецЦикла;

КонецПроцедуры

Процедура ПодключитьСервисыКомпонента()
	
	МенеджерОбъектов = Приложение.МенеджерОбъектов();
	ПутьКСервисам = ОбъединитьПути(ПутьКомпонента, "Сервисы");

	Если НЕ ФС.КаталогСуществует(ПутьКСервисам) Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" отсутствует каталог с сервисами", Имя));
		Возврат;
	КонецЕсли;

	ФайлыСервисов = НайтиФайлы(ПутьКСервисам, "*.os", Истина);

	Если НЕ ФайлыСервисов.Количество() Тогда
		Лог.Отладка(СтрШаблон("В компоненте ""%1"" отсутствуют сервисы", Имя));
	КонецЕсли;

	Для каждого Файл Из ФайлыСервисов Цикл
		КлючОбъекта = Служебное.ПодготовитьКлючОбъекта(Файл, ПутьКСервисам);
		МенеджерОбъектов.ЗарегистрироватьОбъект(Файл.ПолноеИмя, КлючОбъекта);
		Лог.Отладка(СтрШаблон("Загружен сервис ""%1""", Файл.ИмяБезРасширения));
	КонецЦикла;

КонецПроцедуры