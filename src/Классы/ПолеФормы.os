Перем Имя;
Перем ИмяЛатинское;
Перем Заголовок;
Перем ТипЗначения;
Перем Вид;
Перем ЗначениеПоля;
Перем Параметры;

Процедура ПриСозданииОбъекта(Знач ПарИмя, Знач ПарЗаголовок, Знач ПарТип, Знач ПарВид, Знач ПарИмяЛатинское = Неопределено)
	
	Если НЕ Служебное.СоответствуетCamelCase(ПарИмя, Истина) Тогда
		ВызватьИсключение "Имя поля содержит не допустимые символы";
	КонецЕсли;

	Имя = ПарИмя;

	Если ПарИмяЛатинское <> Неопределено И Служебное.СоответствуетCamelCase(ПарИмяЛатинское) Тогда
		ИмяЛатинское = ПарИмяЛатинское;
	ИначеЕсли ПарИмяЛатинское = Неопределено Тогда
		ИмяЛатинское = Сериализация.ЗаменитьСимволы(Имя);
	Иначе
		ВызватьИсключение "Латинское имя поля содержит не допустимые символы";
	КонецЕсли;
	
	Заголовок = ПарЗаголовок;
	ТипЗначения = ПарТип;
	Вид = ПарВид;
	Параметры = Новый Структура();

	Параметры.Вставить("СписокВыбора", Новый СписокЗначений());
	Параметры.Вставить("РежимВводаПароля", Ложь);
	Параметры.Вставить("Обязательно", Ложь);

	Если ТипЗначения = Тип("Строка") Тогда
		ЗначениеПоля = "";
	ИначеЕсли ТипЗначения = Тип("Число") Тогда
		ЗначениеПоля = 0;
	ИначеЕсли ТипЗначения = Тип("Булево") Тогда
		ЗначениеПоля = Ложь;
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		ЗначениеПоля = Дата(1, 1, 1);
	Иначе
		ЗначениеПоля = "";
	КонецЕсли;

КонецПроцедуры

Функция Имя() Экспорт
	Возврат Имя;
КонецФункции

Функция ИмяЛатинское() Экспорт
	Возврат ИмяЛатинское;
КонецФункции

Функция Заголовок() Экспорт
	Возврат Заголовок;
КонецФункции

Функция ТипЗначения() Экспорт
	Возврат ТипЗначения;
КонецФункции

Функция Вид() Экспорт
	Возврат Вид;
КонецФункции

Функция Параметры() Экспорт
	Возврат Параметры;
КонецФункции

Функция Значение(Значение = Неопределено) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат ЗначениеПоля;
	КонецЕсли;
	
	Если ТипЗнч(Значение) <> ТипЗначения Тогда
		ВызватьИсключение "Ожидается тип " + Строка(ТипЗначения);
	КонецЕсли;

	ЗначениеПоля = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ТипHTML() Экспорт
	
	Соответствие = Новый Соответствие();
	Соответствие.Вставить(ВидыПолейФормы.Строка, "text");
	Соответствие.Вставить(ВидыПолейФормы.Число, "number");
	Соответствие.Вставить(ВидыПолейФормы.Флажок, "checkbox");
	Соответствие.Вставить(ВидыПолейФормы.Файл, "file");
	Соответствие.Вставить(ВидыПолейФормы.Скрытый, "hidden");
	Соответствие.Вставить(ВидыПолейФормы.Изображение, "image");
	Соответствие.Вставить(ВидыПолейФормы.Пароль, "password");
	Соответствие.Вставить(ВидыПолейФормы.Переключатель, "radio");
	Соответствие.Вставить(ВидыПолейФормы.Цвет, "color");
	Соответствие.Вставить(ВидыПолейФормы.Дата, "date");
	Соответствие.Вставить(ВидыПолейФормы.ДатаВремя, "datetime");
	Соответствие.Вставить(ВидыПолейФормы.ДатаВремяЛокально, "datetime-local");
	Соответствие.Вставить(ВидыПолейФормы.Почта, "email");
	Соответствие.Вставить(ВидыПолейФормы.Диапозон, "range");
	Соответствие.Вставить(ВидыПолейФормы.Поиск, "search");
	Соответствие.Вставить(ВидыПолейФормы.Телефон, "tel");
	Соответствие.Вставить(ВидыПолейФормы.Время, "time");
	Соответствие.Вставить(ВидыПолейФормы.АдресСайта, "url");
	Соответствие.Вставить(ВидыПолейФормы.Месяц, "month");
	Соответствие.Вставить(ВидыПолейФормы.Неделя, "week");
	
	Возврат Соответствие[Вид];
	
КонецФункции