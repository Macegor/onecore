#Использовать strings
#Использовать fs

Перем КодВыполнения;
Перем ЧастиШаблона;
Перем РамкиБлоков;
Перем СоответствияРамокБлоков;

Перем ТекстШаблона Экспорт;

Процедура ПриСозданииОбъекта()

	РамкиБлоков = Новый Структура();
	РамкиБлоков.Вставить("НачалоВыражения", 	МассивИзДвухСимволов("{", "{"));
	РамкиБлоков.Вставить("ОкончаниеВыражения", 	МассивИзДвухСимволов("}", "}"));
	РамкиБлоков.Вставить("НачалоОператора", 	МассивИзДвухСимволов("{", "%"));
	РамкиБлоков.Вставить("ОкончаниеОператора", 	МассивИзДвухСимволов("%", "}"));

	СоответствияРамокБлоков = Новый Соответствие;
	СоответствияРамокБлоков.Вставить(СтрСоединить(РамкиБлоков.НачалоВыражения, ""), РамкиБлоков.ОкончаниеВыражения);
	СоответствияРамокБлоков.Вставить(СтрСоединить(РамкиБлоков.НачалоОператора, ""), РамкиБлоков.ОкончаниеОператора);

КонецПроцедуры

Процедура ИнициализацияПеременных()

	КодВыполнения = Новый Массив();
	ЧастиШаблона = Новый Соответствие();
	
КонецПроцедуры

Функция Сформировать(КлючШаблона, Данные = Неопределено) Экспорт

	ИнициализацияПеременных();
	ТекстШаблонаПоПути(КлючШаблона);
	
	БлокПоиска = Новый Массив(2);
	ТекущийПоток = Новый Массив;

	ИскомоеОкочание = Неопределено;

	Для Сч = 1 по СтрДлина(ТекстШаблона) Цикл
		ТекущийСимвол = Сред(ТекстШаблона, Сч, 1);
		ДобавитьВМассивСоСмещением(БлокПоиска, ТекущийСимвол);
		
		Если МассивыРавны(БлокПоиска, РамкиБлоков.НачалоВыражения) 
				или МассивыРавны(БлокПоиска, РамкиБлоков.НачалоОператора) Тогда		
			Ид = ДобавитьЧастьШаблона(ТекущийПоток);
			ТекущийПоток = Новый Массив;
			ИскомоеОкочание = ПолучитьЗакрывающийТег(БлокПоиска);
			ЧастьКодаВставкаЧастиШаблона(Ид);

		ИначеЕсли Не ИскомоеОкочание = Неопределено 
					И МассивыРавны(БлокПоиска, ИскомоеОкочание) Тогда
			ЧастьКода = СтрокаИзМассива(ТекущийПоток);
			ТекущийПоток = Новый Массив;
			ДобавитьЧастьКодаОтОкочания(ЧастьКода, ИскомоеОкочание);
			ИскомоеОкочание = Неопределено;	

		Иначе
			ТекущийПоток.Добавить(ТекущийСимвол);
		КонецЕсли;
	КонецЦикла;

	Если ТекущийПоток.Количество() > 0 Тогда
		ТекущийПоток.Добавить(" ");
		Ид = ДобавитьЧастьШаблона(ТекущийПоток);
		ЧастьКодаВставкаЧастиШаблона(Ид);
	КонецЕсли;

	Результат = Новый Массив;

	ЗаполнитьМассивКодом(Результат, Данные);

	ТекстОтображения = СтрСоединить(Результат, "");

	Возврат ТекстОтображения;

КонецФункции

Процедура ЗаполнитьМассивКодом(Результат, Данные)

	ВесьКод = СтрСоединить(КодВыполнения);
	Выполнить(ВесьКод);
	
КонецПроцедуры

Процедура ДобавитьЧастьКодаОтОкочания(ЧастьКода, Окончание)

	Если Окончание = РамкиБлоков.ОкончаниеВыражения Тогда
		КодКДобавлению = СтрШаблон("_Значение = Строка(%1);
									|Результат.Добавить(_Значение);", ЧастьКода);
		КодВыполнения.Добавить(КодКДобавлению + Символы.ПС);
	Иначе
		КодВыполнения.Добавить(ЧастьКода + Символы.ПС);
	КонецЕсли;

КонецПроцедуры

Процедура ЧастьКодаВставкаЧастиШаблона(Ид)

	Код = СтрШаблон("Результат.Добавить(ЧастиШаблона[""%1""]);", Ид);
	КодВыполнения.Добавить(Код + Символы.ПС);
	
КонецПроцедуры

Функция ДобавитьЧастьШаблона(МассивСимволов)

	Строка = СтрокаИзМассива(МассивСимволов);
	Ид = ХешироватьСтроку(Строка);
	
	ЧастиШаблона.Вставить(Ид, Строка);

	Возврат Ид;
	
КонецФункции

Функция СтрокаИзМассива(МассивСимволов)
	МассивСимволов.Удалить(МассивСимволов.ВГраница());
	Строка = СтрСоединить(МассивСимволов, "");
	Возврат Строка; 	
КонецФункции

Функция ХешироватьСтроку(Строка)  
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
	
	Хеширование.Добавить(Строка);
	
	Возврат Base64Строка(Хеширование.ХешСумма);
	
КонецФункции

Функция МассивИзДвухСимволов(Символ1, Символ2)

	Массив = Новый Массив(2);
	Массив[0] = Символ1;
	Массив[1] = Символ2;
	
	Возврат Массив;

КонецФункции

Функция ПолучитьЗакрывающийТег(ОткрывающийТег)

	Возврат СоответствияРамокБлоков.Получить(СтрСоединить(ОткрывающийТег,""));	

КонецФункции

Функция МассивыРавны(Массив1, Массив2) Экспорт

	Если не Массив1.Количество() = Массив2.Количество() Тогда
		Возврат Ложь;
	Иначе
		Для сч = 0 По Массив1.ВГраница() Цикл
			Если Не Массив1[сч] = Массив2[сч] Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

Процедура ДобавитьВМассивСоСмещением(Массив, ДобавляемыйЭлемент) Экспорт
	
	Если Массив.Количество() = 1 Тогда
		Массив[0] = ДобавляемыйЭлемент;
		Возврат;
	ИначеЕсли Массив.Количество() = 0 Тогда
		ВызватьИсключение "Пустой массив для смещения";
	КонецЕсли;

	Для сч = 0 по Массив.ВГраница() - 1 Цикл
		Массив[сч] = Массив[сч + 1];
	КонецЦикла;

	Массив[Массив.ВГраница()] = ДобавляемыйЭлемент;

КонецПроцедуры

Функция Вложить(КлючШаблона, Данные = Неопределено) // BSLLS:UnusedLocalMethod-off

	ВложенныйШаблон = Новый Шаблонизатор(); 
	Возврат ВложенныйШаблон.Сформировать(КлючШаблона, Данные);

КонецФункции

Функция Расширить(КлючШаблона) // BSLLS:FunctionShouldHaveReturn-off
	// TODO: Придумать что то с этим
КонецФункции

Функция Ресурс(КлючРесурса) // BSLLS:UnusedLocalMethod-off
	
	Если СтрНайти(КлючРесурса, ".Ресурсы.") Тогда
		Ключ = СтрЗаменить(КлючРесурса, ".Ресурсы.", ".");
	Иначе
		Ключ = КлючРесурса;
	КонецЕсли;

	ЧастиПутейРесурса = СтроковыеФункции.РазложитьСтрокуВМассивПодстрок(Ключ, ".", Истина, Истина);
	ПутьКФайлуРесурса = "/";

	МенеджерКомпонентов = Приложение.МенеджерКомпонентов();
	Компонент = МенеджерКомпонентов.НайтиПоИмени(ЧастиПутейРесурса[0]);

	Если Компонент = Неопределено Тогда
		Лог.Ошибка("Не верно указано имя компонента в ключе ресурса");
		Возврат "";
	КонецЕсли;

	ЛатинскоеИмяКомпонента = Компонент.ИмяЛатинское();
	ВсегоЧастей = ЧастиПутейРесурса.Количество() - 1;

	Для Счетчик = 0 По ВсегоЧастей Цикл
		Если Счетчик = ВсегоЧастей Тогда
			ПутьКФайлуРесурса = ПутьКФайлуРесурса + "." + ЧастиПутейРесурса[Счетчик];
		ИначеЕсли Счетчик = 0 Тогда
			ПутьКФайлуРесурса = ПутьКФайлуРесурса + ЛатинскоеИмяКомпонента;
		Иначе
			ПутьКФайлуРесурса = ПутьКФайлуРесурса + "/" + ЧастиПутейРесурса[Счетчик];		
		КонецЕсли;
	КонецЦикла;

	Возврат ПутьКФайлуРесурса;

КонецФункции

Процедура ТекстШаблонаПоПути(КлючШаблона)
	
	ТекстШаблона = "";

	Если НЕ СтрНайти(КлючШаблона, ".Шаблоны.") Тогда
		ИмяКомпонента = Лев(КлючШаблона, СтрНайти(КлючШаблона, ".") - 1);
		Ключ = СтрЗаменить(КлючШаблона, ИмяКомпонента, ИмяКомпонента + ".Шаблоны");
	Иначе
		Ключ = КлючШаблона;
	КонецЕсли;

	ЧастиПутейШаблона = СтроковыеФункции.РазложитьСтрокуВМассивПодстрок(Ключ, ".", Истина, Истина);
	ПутьКФайлуШаблона = Служебное.КаталогПриложения();

	Для каждого ЧастьПути Из ЧастиПутейШаблона Цикл
		ПутьКФайлуШаблона = ОбъединитьПути(ПутьКФайлуШаблона, ЧастьПути);
	КонецЦикла;

	ПутьКФайлуШаблона = ПутьКФайлуШаблона + ".html";

	Если НЕ ФС.ФайлСуществует(ПутьКФайлуШаблона) Тогда
		ВызватьИсключение СтрШаблон("Не найден файл шаблона по пути: %1", ПутьКФайлуШаблона);
	КонецЕсли;

	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФайлуШаблона);
	ТекстШаблона = ТекстовыйДокумент.ПолучитьТекст();

КонецПроцедуры