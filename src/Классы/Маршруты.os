
Перем СписокМаршрутов;
Перем Компонент;

Процедура ПриСозданииОбъекта(_Компонент = Неопределено)

	Компонент = _Компонент;

	СписокМаршрутов = Новый ТаблицаЗначений();
	СписокМаршрутов.Колонки.Добавить("Адрес", Новый ОписаниеТипов("Строка"));
	СписокМаршрутов.Колонки.Добавить("КлючОбъекта", Новый ОписаниеТипов("Строка"));
	СписокМаршрутов.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	СписокМаршрутов.Колонки.Добавить("Статический", Новый ОписаниеТипов("Булево"));

КонецПроцедуры

Процедура Добавить(Адрес, КлючОбъекта, Статический = Ложь) Экспорт
	
	МенеджерОбъектов = Приложение.МенеджерОбъектов();

	Если Компонент <> Неопределено И НЕ Статический Тогда
		КлючОбъекта = СтрШаблон("%1.%2.%3", Компонент.Имя(), "Представления", КлючОбъекта);
	КонецЕсли;

	Тип = МенеджерОбъектов.ТипПоКлючу(КлючОбъекта);

	Если Тип = Неопределено Тогда
		Лог.Предупреждение(СтрШаблон("Не найден обработчик маршрута %1 по ключу %2, маршрут не добавлен", Адрес, КлючОбъекта));
		Возврат;
	КонецЕсли;

	Если НЕ АдресСвободен(Адрес) Тогда
		Лог.Предупреждение(СтрШаблон("Добавляемый адрес %1 занят другим обработчиком, маршрут не добавлен", Адрес));
		Возврат;
	КонецЕсли;

	НоваяСтрока = СписокМаршрутов.Добавить();
	НоваяСтрока.Адрес = Адрес;
	НоваяСтрока.КлючОбъекта = КлючОбъекта;
	НоваяСтрока.Тип = Тип;
	НоваяСтрока.Статический = Статический;

	Если Компонент <> Неопределено Тогда
		Лог.Отладка(СтрШаблон("Добавлен маршрут %1, ключ объекта обработчика: %2", Адрес, КлючОбъекта));
	КонецЕсли;
	
КонецПроцедуры

Функция СписокМаршрутов() Экспорт
	Возврат СписокМаршрутов;
КонецФункции

Функция НайтиМаршрут(Адрес) Экспорт
	
	НайденныеСтроки = СписокМаршрутов.НайтиСтроки(Новый Структура("Адрес", Адрес));	
	Возврат ?(НайденныеСтроки.Количество(), НайденныеСтроки[0], Неопределено);

КонецФункции

Функция АдресСвободен(Адрес)
	
	НайденныеСтроки = СписокМаршрутов.НайтиСтроки(Новый Структура("Адрес", Адрес));
	Возврат НЕ НайденныеСтроки.Количество();

КонецФункции