
Перем ТаблицаОбъектов;

Процедура ПриСозданииОбъекта()
	
	ТаблицаОбъектов = Новый ТаблицаЗначений();
	ТаблицаОбъектов.Колонки.Добавить("Тип");
	ТаблицаОбъектов.Колонки.Добавить("ТипСтрокой");
	ТаблицаОбъектов.Колонки.Добавить("Ключ");

КонецПроцедуры

Процедура ЗарегистрироватьОбъект(Путь, Ключ) Экспорт
	
	ИмяТипа = ИмяТипаПоКлючу(Ключ);

	Попытка
		ПодключитьСценарий(Путь, ИмяТипа);
	Исключение
		Лог.Ошибка(СтрШаблон("Ошибка при регистрации объекта %1: %2", Ключ, ОписаниеОшибки()));
	КонецПопытки;

	НоваяСтрока = ТаблицаОбъектов.Добавить();
	НоваяСтрока.Тип = Тип(ИмяТипа);
	НоваяСтрока.ТипСтрокой = ИмяТипа;
	НоваяСтрока.Ключ = Ключ;

КонецПроцедуры

Функция СоздатьОбъект(Ключ, Параметры = Неопределено, ЗаполненияСвойств = Неопределено) Экспорт
	
	Тип = ТипПоКлючу(Ключ);
	Объект = Новый(Тип, Параметры);

	Если ЗаполненияСвойств.Количество() Тогда		
		Для каждого КлючЗначение Из ЗаполненияСвойств Цикл
			Если Рефлексия.СвойствоСуществует(Объект, КлючЗначение.Ключ) Тогда
				Рефлексия.УстановитьСвойство(Объект, КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	// TODO: Реализовать внедрение зависимостей по декларативному методу

	Возврат Объект;

КонецФункции

Функция ИмяТипаПоКлючу(Ключ) Экспорт

	Возврат СтрЗаменить(ТРег(СтрЗаменить(Ключ, ".", " ")), " ", "");
	
КонецФункции

Функция ОбъектЗарегистрирован(Ключ) Экспорт
	
	Возврат ЗаписьПоКлючу(Ключ) <> Неопределено;

КонецФункции

Функция ТипПоКлючу(Ключ) Экспорт
	
	Запись = ЗаписьПоКлючу(Ключ);

	Если Запись = Неопределено Тогда
		Возврат Запись;
	КонецЕсли;

	Возврат Запись.Тип;

КонецФункции

Функция ЗаписьПоКлючу(Ключ)
	
	НайденныеСтроки = ТаблицаОбъектов.НайтиСтроки(Новый Структура("Ключ", Ключ));

	Если Не НайденныеСтроки.Количество() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат НайденныеСтроки[0];

КонецФункции