
Перем МетодЗапроса;
Перем АдресСервера;
Перем ТелоЗапроса;
Перем ПараметрыАдреса;
Перем ЗаголовкиЗапроса;
Перем Соединение;

Процедура ПриСозданииОбъекта(Метод, Сервер, ЗащищенноеСоединение = Ложь, Порт = Неопределено, ТаймаутСоединения = 0, Пользователь = Неопределено, Пароль = Неопределено)
	
	МетодЗапроса = Метод;
	АдресСервера = Сервер;
	ЗаголовкиЗапроса = Новый Соответствие();
	ПараметрыАдреса = Новый Соответствие();

	Если Порт = Неопределено Тогда
		Порт = ?(ЗащищенноеСоединение, 443, 80);
	КонецЕсли;

	// Формируем соединение с сервером.
	Соединение = Новый HTTPСоединение(?(ЗащищенноеСоединение, "https://", "http://") + АдресСервера, // сервер (хост)
	Порт, // порт, по умолчанию для http используется 80, для https 443
	Пользователь, // пользователь для доступа к серверу (если он есть)
	Пароль, // пароль для доступа к серверу (если он есть)
	, // здесь указывается прокси, если он есть
	ТаймаутСоединения); // таймаут в секундах, 0 или пусто - не устанавливать


КонецПроцедуры

Функция Параметры() Экспорт
	Возврат ПараметрыАдреса;
КонецФункции

Функция Заголовки() Экспорт
	Возврат ЗаголовкиЗапроса;
КонецФункции

Процедура УстановитьТело(ТипКонтента, ДанныеТела) Экспорт
	
	ЗаголовкиЗапроса.Вставить("Content-Type", ВебСерверСлужебное.ОпределитьТипСодержимого(ТипКонтента));
	ТелоЗапроса = ДанныеТела;

КонецПроцедуры

// Отправляет запрос по указанным параметрам
// 
// Параметры:
//  АдресРесурса - Строка - Адрес без указания сервера, например: "/users" или "/" если нужен корень
//  ИмяВыходногоФайла - Неопределено, Строка - Если задан, то тело ответа будет записано в файл. В противном случае, 
//                                             тело ответа можно получить методами ПолучитьТелоКакСтроку и ПолучитьТелоКакДвоичныеДанные 
// 
// Возвращаемое значение:
//  Структура - Результат выполнения:
//		* Статус - Булево - Истина, если запрос выполнен успешно
//		* ИнформацияОбОшибке - ИнформацияОбОшибке - Заполняется если возникла ошибка в процессе выполнения запроса
//		* РезультатЗапроса - HTTPОтвет, Неопределено - Ответ от сервера или неопределено, если возникла ошибка
Функция Отправить(Знач АдресРесурса, Знач ИмяВыходногоФайла = Неопределено) Экспорт
			
	// Формируем параметры запроса
	Если ТипЗнч(ПараметрыАдреса) = Тип("Соответствие") Тогда
		СтрокаПараметров = "?";
		Для Каждого КлючЗначение Из ПараметрыАдреса Цикл
			СтрокаПараметров = СтрокаПараметров + ?(СтрокаПараметров = "?", "", "&") + КлючЗначение.Ключ + "="
			+ КлючЗначение.Значение;
		КонецЦикла;
	Иначе
		СтрокаПараметров = "";
	КонецЕсли;
	
	// Создаём запрос
	Запрос = Новый HTTPЗапрос(АдресРесурса + СтрокаПараметров, ЗаголовкиЗапроса);
	
	Если ТипЗнч(ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда	
		Запрос.УстановитьТелоИзДвоичныхДанных(ТелоЗапроса);
	ИначеЕсли ТипЗнч(ТелоЗапроса) = Тип("Строка") Тогда
		Запрос.УстановитьТелоИзСтроки(ТелоЗапроса);
	Иначе
		Лог.Предупреждение("Не указано тело запроса");
	КонецЕсли;

	СтруктураРезультата = Новый Структура("Статус, ИнформацияОбОшибке, РезультатЗапроса");
	
	// Выполняем запрос
	Попытка
		СтруктураРезультата.РезультатЗапроса = Соединение.ВызватьHTTPМетод(МетодЗапроса, Запрос, ИмяВыходногоФайла);
		СтруктураРезультата.Статус = Истина;
	Исключение
		СтруктураРезультата.Статус = Ложь;
		ВызватьИсключение "При отправке запроса возникла ошибка: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат СтруктураРезультата;

КонецФункции