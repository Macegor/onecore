
#Использовать strings

Перем ТекущиеСеансы;

Процедура ПриСозданииОбъекта()
	
	ТекущиеСеансы = Новый ТаблицаЗначений();
	ТекущиеСеансы.Колонки.Добавить("Идентификатор");
	ТекущиеСеансы.Колонки.Добавить("Сеанс");

КонецПроцедуры

Функция СоздатьСеанс() Экспорт
	
	Сеанс = Новый Сеанс();
	Идентификатор = Сеанс.ПолучитьИдентификатор();

	СтрокаСеанса = ТекущиеСеансы.Добавить();
	СтрокаСеанса.Идентификатор = Идентификатор;
	СтрокаСеанса.Сеанс = Сеанс;

	Возврат Сеанс;

КонецФункции

Функция ОпределитьСеанс(Контекст) Экспорт

	Куки = ПолучитьКуки(Контекст.Запрос);
	Сеанс = Неопределено;

	Если Куки <> Неопределено Тогда
		ИдентификаторСеанса = Куки["session_id"];
		Сеанс = НайтиСеанс(ИдентификаторСеанса);	
	КонецЕсли;

	Если Сеанс = Неопределено Тогда
		Сеанс = СоздатьСеанс();
		ДобавитьКуку(Контекст.Ответ, "session_id", Сеанс.ПолучитьИдентификатор(), Истина, Ложь, Сеанс.ПолучитьВремяЖизни());
	КонецЕсли;

	Возврат Сеанс;

КонецФункции

Функция НайтиСеанс(Идентификатор)
	
	НайденныеСтроки = ТекущиеСеансы.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
	Возврат ?(НайденныеСтроки.Количество(), НайденныеСтроки[0].Сеанс, Неопределено);

КонецФункции

Процедура ДобавитьКуку(Ответ, Ключ, Значение, Защищена = Истина, ТолькоЗапросы = Ложь, ВремяЖизни = Неопределено) Экспорт
	
	Ключ = Строка(Ключ);
	Значение = Строка(Значение);
	Значение = ?(ВремяЖизни = Неопределено, Значение, Значение + "; Max-Age=" + Формат(ВремяЖизни, "ЧГ=0"));
	Значение = ?(Защищена, Значение + "; SameSite=Strict", Значение);
	Значение = ?(ТолькоЗапросы, Значение + "; HttpOnly", Значение);

	Ответ.Заголовки.Добавить("Set-Cookie", Ключ + "=" + Значение);

КонецПроцедуры

Функция ПолучитьКуки(Запрос) Экспорт
	
	Куки = Новый Соответствие();
	КукиСтрокой = "";

	Для каждого Заголовок Из Запрос.Заголовки Цикл
		Если Заголовок.Ключ = "Cookie" Тогда
			КукиСтрокой = Заголовок.Значение;
			Прервать;	
		КонецЕсли;
	КонецЦикла;

	Если Не ЗначениеЗаполнено(КукиСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Массив = СтроковыеФункции.РазложитьСтрокуВМассивПодстрок(КукиСтрокой, ";", Истина, Истина);

	Для каждого Элемент Из Массив Цикл

		КлючЗначение = СтроковыеФункции.РазложитьСтрокуВМассивПодстрок(Элемент, "=", Истина, Истина);

		Если КлючЗначение.Количество() < 2 Тогда
			Продолжить;
		КонецЕсли;

		Куки.Вставить(КлючЗначение[0], КлючЗначение[1]);

	КонецЦикла;

	Возврат Куки;

КонецФункции
