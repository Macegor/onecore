
#Использовать logos

Перем ЛогФреймворка;

// Устанавливает минимально выводимый в консоль лог
//
// Параметры:
//	Уровень - Строка - Доступные варианты: "Отладка", "Информация", "Предупреждение", "Ошибка", "КритичнаяОшибка"
Процедура Инициализировать(Уровень = "Информация") Экспорт
	
	УстанавливаемыйУровень = УровеньЛогаПоИмени(Уровень);

	Если УстанавливаемыйУровень = Неопределено Тогда
		УстанавливаемыйУровень = УровниЛога.Информация;
	КонецЕсли;

	ЛогФреймворка.УстановитьУровень(УстанавливаемыйУровень);
	ЛогФреймворка.УстановитьРаскладку(ЭтотОбъект);

КонецПроцедуры

Процедура Отладка(Сообщение) Экспорт
	ЛогФреймворка.Отладка(Сообщение);
КонецПроцедуры

Процедура Информация(Сообщение) Экспорт
	ЛогФреймворка.Информация(Сообщение);
КонецПроцедуры

Процедура Предупреждение(Сообщение) Экспорт
	ЛогФреймворка.Предупреждение(Сообщение);
КонецПроцедуры

Процедура Ошибка(Сообщение) Экспорт
	ЛогФреймворка.Ошибка(Сообщение);
КонецПроцедуры

Процедура КритичнаяОшибка(Сообщение) Экспорт
	ЛогФреймворка.КритичнаяОшибка(Сообщение);
КонецПроцедуры

Функция ПолучитьФорматированноеСообщение(Знач СобытиеЛога) Экспорт
   
	// СобытиеЛога - Объект с методами
	//   * ПолучитьУровень() - Число - уровень лога
	//   * ПолучитьСообщение() - Строка - текст сообщения
	//   * ПолучитьИмяЛога() - Строка - имя лога
	//   * ПолучитьВремяСобытия() - Число - Универсальная дата-время события в миллисекундах
	//   * ПолучитьДополнительныеПоля() - Соответствие - дополнительные поля события

	ДатаСобытия = ТекущаяДата();
	ТекстСобытия = СобытиеЛога.ПолучитьСообщение();
	ПредставлениеУровня = ТРег(УровниЛога.НаименованиеУровня(СобытиеЛога.ПолучитьУровень()));
	ФорматированноеСообщение = СтрШаблон("[%1][%2]: %3", Формат(ДатаСобытия, "ДФ='HH:mm:ss'"), ПредставлениеУровня, ТекстСобытия);
 
	Возврат ФорматированноеСообщение;
 
 КонецФункции

 Функция УровеньЛогаПоИмени(ИмяЛога)

	УровниПриоритета = Новый Соответствие();
	УровниПриоритета.Вставить("Отладка"        , 0);
	УровниПриоритета.Вставить("Информация"     , 1);
	УровниПриоритета.Вставить("Предупреждение" , 2);
	УровниПриоритета.Вставить("Ошибка"         , 3);
	УровниПриоритета.Вставить("КритичнаяОшибка", 4);

	Возврат УровниПриоритета[ИмяЛога];

 КонецФункции

ЛогФреймворка = Логирование.ПолучитьЛог("onecore.app.message");